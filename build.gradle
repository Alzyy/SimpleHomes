plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta13'
}

description = 'SimpleHomes'
group = 'it.alzy.simplehomes'
version = '1.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url "https://jitpack.io" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    implementation 'com.github.NaTorOG:SimpleConfig:1.0.6'
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation "com.zaxxer:HikariCP:7.0.0"
    implementation "dev.triumphteam:triumph-gui:3.1.11"
    compileOnly("com.mojang:authlib:1.5.25")
}
 
def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}


shadowJar {
    minimize()
    mergeServiceFiles()
    archiveFileName = "${project.description}.jar"
    relocate 'co.aikar.commands', 'it.alzy.simplehomes.libs.acf'
    relocate 'co.aikar.locales', 'it.alzy.simplehomes.libs.locales'
    relocate("dev.triumphteam.gui", "it.alzy.simplehomes.gui")
}